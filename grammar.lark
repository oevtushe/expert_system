start: file

file: (rule NEWLINE)* defines EOLS questions NEWLINE*

rule: expr_xor IMPLIES expr_xor

expr_xor: expr_xor "^" expr_or
     | expr_or

expr_or: expr_or "|" expr_and
    | expr_and

expr_and: expr_and "+" expr_neg
     | expr_neg

expr_neg: BANG factor
     | factor

factor: val | "(" expr_xor ")"

val: PROPOSITION

defines: "="PROPOSITION*

questions: "?"PROPOSITION+

PROPOSITION: /[A-Z]/
IMPLIES: "=>"
EOLS: NEWLINE+
BANG: "!"
COMMENT: /#.*/ EOLS

%ignore " "
%ignore COMMENT
%import common.NEWLINE
